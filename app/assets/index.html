<!DOCTYPE html>
<html>
<head>
  <meta http-equiv="Content-type" content="text/html; charset=utf-8">
  <title>WebGL Particles</title>
</head>
<body id="index">
  <input id="slider1" type="range" min="100" max="500" step="10" />
  <canvas id="glcanvas" style="height: 100vh; width: 100vw;">

    Your browser doesn't appear to support the <code>&lt;canvas&gt;</code> element.
  </canvas>
  
  <script id="particle_sim_vs" type="x-shader/x-vertex">
  #ifdef GL_ES
  precision highp float;
  #endif

  attribute vec3 aPosition;

  void main() {
    gl_Position = vec4(aPosition, 1.0);
  }
  </script>
  
  <script id="particle_sim_fs" type="x-shader/x-fragment">
  #extension GL_EXT_draw_buffers : require
  precision mediump float;
  
  uniform vec2 uResolution;
  uniform float nowTime;
  uniform float deltaTime;

  uniform vec3 gravityVal;
  uniform int gravityType;
  uniform float gravityPower;
  uniform float friction;

  uniform sampler2D uTexture0; // x, y, z, birth
  uniform sampler2D uTexture1; // birth r, g, b, lifetime
  uniform sampler2D uTexture2; // dx, dy, dz, sz
  uniform sampler2D uTexture3; // death r, g, b, ?

  void main() {
    vec2 uv = gl_FragCoord.xy / uResolution.xy;

    vec4 pdata0 = texture2D(uTexture0, uv);
    vec4 pdata1 = texture2D(uTexture1, uv);
    vec4 pdata2 = texture2D(uTexture2, uv);
    gl_FragData[3] = texture2D(uTexture3, uv);
    
    float birth = pdata0.w;
    float lifetime = pdata1.w;
    float ttl = birth + lifetime - nowTime;
    if (ttl < 0.0) {
      gl_FragData[0] = vec4(0.0);
      gl_FragData[1] = vec4(0.0);
      gl_FragData[2] = vec4(0.0);
    } else {
      vec3 pos = pdata0.xyz + pdata2.xyz * deltaTime / 1000.0;
      gl_FragData[0] = vec4(pos, pdata0.w);

      gl_FragData[1] = pdata1;
    
      vec3 velocity;
      if (gravityType == 0) {
        vec3 gravityPull = (gravityVal - pdata0.xyz) / gravityPower;
        velocity = vec3(gravityPull + pdata2.xyz * friction);
      } else {
        velocity = vec3(pdata2.xyz * friction + gravityVal);
      }
      gl_FragData[2] = vec4(velocity, pdata2.w);
    }
  }
  </script>
  
  <script id='particle_vs' type="x-shader/x-vertex">
  // attribute vec2 vertexPosition;
  attribute vec2 aUV;
  varying vec3 v3xyz;
  varying vec2 vUV;

  uniform float nowTime;
  uniform float deltaTime;

  uniform sampler2D uTexture0; // x, y, z, birth
  uniform sampler2D uTexture1; // r, g, b, lifetime
  uniform sampler2D uTexture2; // dx, dy, dz, sz
  uniform sampler2D uTexture3; // death r, g, b, ?
  
  void main() {
    vec4 pdata0 = texture2D(uTexture0, aUV);
    vec4 pdata1 = texture2D(uTexture1, aUV);
    vec4 pdata2 = texture2D(uTexture2, aUV);

    v3xyz = pdata0.xyz;
    gl_Position = vec4(v3xyz, 1.0);

    float birth = pdata0.w;
    float lifetime = pdata1.w;
    float death = birth + lifetime;
    
    if (death > nowTime) {
      // float ttl = death - nowTime;
      float age = (nowTime - birth) / lifetime;
      float size = pdata2.w;

      if (age < 0.1) {
        gl_PointSize = size * age * 10.0;
      } else
      if (age > 0.9) {
        gl_PointSize = size * (1.0 - age) * 10.0;
      } else
      {
        gl_PointSize = size;
      }
    } else {
      gl_PointSize = 1.0;
    }
    
    vUV = aUV;
  }
  </script>
  
  <script id='particle_fs' type="x-shader/x-fragment">
  #ifdef GL_ES
  precision highp float;
  #endif

  varying vec3 v3xyz;
  varying vec2 vUV;

  uniform float nowTime;
  uniform float deltaTime;
  
  uniform sampler2D uTexture0; // x, y, z, birth
  uniform sampler2D uTexture1; // r, g, b, lifetime
  uniform sampler2D uTexture2; // dx, dy, dz, sz
  uniform sampler2D uTexture3; // death r, g, b, ?

  void main() {
    vec4 pdata0 = texture2D(uTexture0, vUV);
    vec4 pdata1 = texture2D(uTexture1, vUV);
    vec4 pdata3 = texture2D(uTexture3, vUV);

    float birth = pdata0.w;
    float lifetime = pdata1.w;
    float ttl = birth + lifetime - nowTime;
    if (ttl <= 0.0) {
      discard;
    }
    vec3 birthCol = pdata1.rgb;
    vec3 deathCol = pdata3.rgb;
    float pct = ttl / lifetime;
    gl_FragColor = vec4(mix(birthCol, deathCol, pct), 1.0);
  }
  </script>
  
  <script src="app.js"></script>
  <script>require('app').init();</script>
  
</body>
</html>