<!DOCTYPE html>
<html>
<head>
  <meta http-equiv="Content-type" content="text/html; charset=utf-8">
  <title>WebGL Particles</title>
</head>
<body id="index">
  <div id="app">
  </div>
  
  <script id="particle_sim_vs" type="x-shader/x-vertex">
  #ifdef GL_ES
  precision highp float;
  #endif

  attribute vec3 aPosition;

  void main() {
    gl_Position = vec4(aPosition, 1.0);
  }
  </script>
  
  <script id="particle_sim_fs" type="x-shader/x-fragment">
  #extension GL_EXT_draw_buffers : require
  precision mediump float;
  
  uniform vec2 uResolution;
  uniform float nowTime;
  uniform float deltaTime;

  uniform vec3 gravityVal;
  uniform int gravityType;
  uniform float friction;

  uniform sampler2D uTexture0; // x, y, z
  uniform sampler2D uTexture1; // birth r, g, b, time
  uniform sampler2D uTexture2; // dx, dy, dz, sz
  uniform sampler2D uTexture3; // death r, g, b, time

  void main() {
    vec2 uv = gl_FragCoord.xy / uResolution.xy;

    vec4 pdata0 = texture2D(uTexture0, uv);
    vec4 pdata1 = texture2D(uTexture1, uv);
    vec4 pdata2 = texture2D(uTexture2, uv);
    vec4 pdata3 = texture2D(uTexture3, uv);
    
    float birth = pdata1.w;
    float death = pdata3.w;
    
    if (birth == 0.0 || death > 0.0 && nowTime > death) {
      gl_FragData[0] = vec4(0.0);
      gl_FragData[1] = vec4(0.0);
      gl_FragData[2] = vec4(0.0);
      gl_FragData[3] = vec4(0.0);
    } else {

      float lifetime = death - birth;
      float age = nowTime - birth;
      float age_t = age / lifetime;
      float size = pdata2.w;

      vec3 velocity = pdata2.xyz;

      vec3 pos = pdata0.xyz + velocity * deltaTime / 1000.0;
      gl_FragData[0] = vec4(pos, pdata0.w);
      gl_FragData[1] = pdata1;
    
      if (gravityType == 0) {
        // POINT
        vec3 gravityPull = normalize(gravityVal - pdata0.xyz) * deltaTime / 1000.0;
        velocity = vec3((velocity + gravityPull) * friction);
      } else {
        // VECTOR
        velocity = vec3(velocity + gravityVal) * deltaTime / 1000.0 * friction;
      }

      gl_FragData[2] = vec4(velocity, size);
      gl_FragData[3] = pdata3;
    }
  }
  </script>
  
  <script id='particle_vs' type="x-shader/x-vertex">
  // attribute vec2 vertexPosition;
  attribute vec2 aUV;
  varying vec2 vUV;

  uniform float nowTime;
  uniform float deltaTime;

  uniform sampler2D uTexture0; // x, y, z
  uniform sampler2D uTexture1; // birth r, g, b, time
  uniform sampler2D uTexture2; // dx, dy, dz, sz
  uniform sampler2D uTexture3; // death r, g, b, time
  
  void main() {
    vec4 pdata0 = texture2D(uTexture0, aUV);
    vec4 pdata1 = texture2D(uTexture1, aUV);
    vec4 pdata2 = texture2D(uTexture2, aUV);
    vec4 pdata3 = texture2D(uTexture3, aUV);

    vec3 v3xyz = pdata0.xyz;
    
    
    gl_Position = vec4(v3xyz, 1.0);

    float birth = pdata1.w;
    float death = pdata3.w;
    
    float size = pdata2.w;
    
    if (birth == 0.0 || death > 0.0 && nowTime > death) {
      gl_PointSize = 100.0;
    } else {
      
      float lifetime = death - birth;
      float age = nowTime - birth;
      float age_t = age / lifetime;
      
      // gl_PointSize = floor((1.0 - age_t) * 10.0) + 1.0;

      if (age_t < 0.1) {
        gl_PointSize = size * age_t * 10.0; //  / v3xyz.z;
      } else
      if (age_t > 0.9) {
        gl_PointSize = size * (1.0 - age_t) * 10.0; //  / v3xyz.z;
      } else
      {
        gl_PointSize = size; // / v3xyz.z;
      }
    } 
    
    vUV = aUV;
  }
  </script>
  
  <script id='particle_fs' type="x-shader/x-fragment">
  #ifdef GL_ES
  precision highp float;
  #endif

  varying vec3 v3xyz;
  varying vec2 vUV;

  uniform float nowTime;
  uniform float deltaTime;
  
  uniform sampler2D uTexture0; // x, y, z
  uniform sampler2D uTexture1; // birth r, g, b, time
  uniform sampler2D uTexture2; // dx, dy, dz, sz
  uniform sampler2D uTexture3; // death r, g, b, time

  void main() {
    vec4 pdata0 = texture2D(uTexture0, vUV);
    vec4 pdata1 = texture2D(uTexture1, vUV);
    vec4 pdata2 = texture2D(uTexture2, vUV);
    vec4 pdata3 = texture2D(uTexture3, vUV);
    
    float birth = pdata1.w;
    float death = pdata3.w;

    if (birth == 0.0 || death > 0.0 && nowTime > death) {
      discard;
    }

    float lifetime = death - birth;
    float age = nowTime - birth;

    float age_t = age / lifetime;    
    vec3 birthCol = pdata1.rgb;
    vec3 deathCol = pdata3.rgb;
    
    gl_FragColor = vec4(mix(birthCol, deathCol, age_t), 1.0);
  }
  </script>

  <link rel="stylesheet" type="text/css" href="app.css" media="screen" />
  <script src="app.js"></script>
  <script>require('ui');</script>
  
</body>
</html>